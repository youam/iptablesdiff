#!/usr/bin/perl

# iptablesdiff -- compare two iptables-save rulesets
# call as   $0  ruleset1 ruleset2
# depends: libyaml-syck-perl libalgorithm-diff-perl

# Copyright (c) 2010-2014 Uli Martens <uli@youam.net>
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
# 3. The name of the author may not be used to endorse or promote products
#    derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR IMPLIED
# WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.  IN NO
# EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
# PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
# OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
# OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
# ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use strict;
use warnings;
use YAML::Syck;
use Algorithm::Diff;
use v5.10;

# this is a list of iptables rules which are omitted by iptables-save if
# they're unchanged because they're probably not even loaded
my $implicit_rules = q{
*nat
:INPUT ACCEPT [0:0]
:OUTPUT ACCEPT [0:0]
:PREROUTING ACCEPT [0:0]
:POSTROUTING ACCEPT [0:0]
*filter
:FORWARD ACCEPT [0:0]
:INPUT ACCEPT [0:0]
:OUTPUT ACCEPT [0:0]
*mangle
:PREROUTING ACCEPT [0:0]
:INPUT ACCEPT [0:0]
:FORWARD ACCEPT [0:0]
:OUTPUT ACCEPT [0:0]
:POSTROUTING ACCEPT [0:0]
};

sub canonicalize_service {
	my $service = shift;

	my $SERVICE = {
		ssh => 22,
		smtp => 25,
		domain => 53,
		http => 80,
		pop3 =>  110,
		ntp => 123,
		imap2 => 143,
		https => 443,
		bgp => 179,
		imaps => 993,
		pop3s => 995,
		nrpe => 5666,
		git => 9418,
	};

	return $SERVICE->{$service} if defined $SERVICE->{$service};

	return $service;
}
sub canonicalize_services {
	my $srv = shift;

	my @srvs = sort { $a <=> $b } map { canonicalize_service( $_ ) } split /,/, $srv;

	return join ( ",", @srvs );
}

sub normalize_iptablesrule {
	my $rule = shift;

	# regex snippets
	my $dev = '[a-z0-9_]+';
	my $ip  = '[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+';
	my $net = $ip.'/[0-9]+';
	my $port = '[0-9]+|[0-9]+:[0-9]+';

	$rule =~ s/^/ /;
	$rule =~ s/$/ /;

	# shorten a few commands
	$rule =~ s/ --source / -s /;
	$rule =~ s/ --destination / -d /;
	$rule =~ s/ --out-interface / -o /;
	$rule =~ s/ --in-interface / -i /;
	$rule =~ s/ --jump / -j /;
	$rule =~ s/ --protocol / -p /;
	$rule =~ s/ --match / -m /g;
	$rule =~ s/ --destination-ports / --dports /;

	# bring into normalized order
	$rule =~ s# --state RELATED,ESTABLISHED # --state ESTABLISHED,RELATED #;

	# normalize single IP addresses to addr/net form
	$rule =~ s#-d ($ip) #-d $1/32 #;
	$rule =~ s#-s ($ip) #-s $1/32 #;

	# always quote arguments
	$rule =~ s# --comment ([a-z0-9_-]+) # --comment "$1" #;

	# FIXME this should be done for all services by looking up getent info
	$rule =~ s/ --dport ssh / --dport 22 /;
	$rule =~ s/ --dport smtp / --dport 25 /;
	$rule =~ s/ --dport domain / --dport 53 /;
	$rule =~ s/ --dport http / --dport 80 /;
	$rule =~ s/ --dport pop3 / --dport 110 /;
	$rule =~ s/ --dport imap2 / --dport 143 /;
	$rule =~ s/ --dport https / --dport 443 /;
	$rule =~ s/ --dport imaps / --dport 993 /;
	$rule =~ s/ --dport pop3s / --dport 995 /;
	$rule =~ s/ --dport nrpe / --dport 5666 /;
	$rule =~ s/ --dport git / --dport 9418 /;

	$rule =~ s# --limit ([0-9]+)/s # --limit $1/sec #;

	# set default values where optional and omitted
	$rule =~ s/ -j REJECT\s*$/ -j REJECT --reject-with icmp-port-unreachable/;

	# this normalizes the wrong way, but it's better readable this way
	$rule =~ s/ -m icmp --icmp-type 8 / --icmp-type echo-request /;

	# don't explicitly use -m (tcp|udp|icmp) module when limiting to that protocol
	$rule =~ s# -p (tcp|udp|icmp)(( .*)?)-m \1 # -p $1$2 #;

	# parse out some stuff and put it back together again
	my $chain;
	if ( $rule =~ s/ -A ([a-zA-Z0-9_-]+) / / ) {
		$chain = " -A $1 ";
	}

	my $interface;
	if ( $rule =~ s/ -i ($dev) / / ) {
		$interface = " -i $1 ";
	}

	my $outerface;
	if ( $rule =~ s/ -o ($dev) / / ) {
		$outerface = " -o $1 ";
	}

	my $source;
	if ( $rule =~ s/ ((\! )?-s ($net)) / / ) {
		$source = " $1 ";
	}

	my $dest;
	if ( $rule =~ s/ ((\! )?-d ($net)) / / ) {
		$dest = " $1 ";
	}

	my $sport;
	if ( $rule =~ s/ (--sport ($port)) / / ) {
		$sport = " $1 ";
	}

	my $dport;
	if ( $rule =~ s/ (--dport ($port)) / / ) {
		$dport = " $1 ";
	}

	$rule = ( $chain // "" )
	      . ( $interface // "" )
	      . ( $outerface // "" )
	      . ( $source // "" )
	      . ( $dest // "" )
	      . ( $sport // "" )
	      . ( $dport // "" )
	      . ( $rule );

#	say "debug: $rule";

	$rule =~ s#  +# #g;
	$rule =~ s# $##;
	$rule =~ s#^ ##;

	return $rule;
}

sub parse_ruleset {
	my $file = shift;

	my @rules;
	push @rules, split( "\n", $implicit_rules );

	open my $FILE, '<', $file
		or die "$0: can't open file $file: $!";
	push @rules, <$FILE>;
	close $FILE;

	my $ruleset;
	my $table;
	for ( @rules ) {
		chomp;
		next if m/^#/;
		next if m/^COMMIT/;
		s/ *$//;
		next if m/^$/;

		$_ = normalize_iptablesrule( $_ );

		if ( m/^\*([a-zA-Z0-9_-]+)$/ ) {
			# this is a table
			$ruleset->{$1} = {} unless defined $ruleset->{$1};
			$table = $1;
			next;
		}
		if ( m/^:([a-zA-Z0-9_-]+) (ACCEPT|DROP|-) \[[0-9]+:[0-9]+\]$/ ) {
			my $chain = $1;
			my $policy = $2;
			$ruleset->{$table}->{$chain}->{policy}=$policy;
			$ruleset->{$table}->{$chain}->{rules} = [];
			next;
		}
		if ( m/^-A ([a-zA-Z0-9_-]+) (.*)$/ ) {
			my $chain = $1;
			my $rule  = $2;
			push @{$ruleset->{$table}->{$chain}->{rules}}, $rule;
			next;
		}
		print STDERR "ignored: $_\n";
	}
	return $ruleset;
}

sub compare_chains {
	my $table = shift;
	my $chain = shift;
	my $c1 = shift;
	my $c2 = shift;

	if ( defined $c1 and not defined $c2 ) {
		print "; iptables -t $table -X $chain\n";
		return;
	} elsif ( not defined $c1 and defined $c2 ) {
		print "; iptables -t $table -N $chain\n";
		$c1->{rules} = [];
	} elsif ( $c1->{policy} ne $c2->{policy} ) {
		print "; iptables -t $table -P $chain $c2->{policy}\n";
	}
	my $diff = Algorithm::Diff->new( $c1->{rules}, $c2->{rules} );
	my $same=1;
	while ( $diff->Next() ) {
		if ( !$diff->Same() ) {
			$same=0;
			last;
		}
	}
	$diff->Reset();
	if ( $same ) {
		#print "# rules are unchanged\n";
	} else {
		print "# rules diff for table $table, chain $chain\n";
		while(  $diff->Next()  ) {
			if ( $diff->Same() ) {
				print "  $_\n" for $diff->Items(1);
				next;
			}
			print "- $_\n"   for  $diff->Items(1);
			print "+ $_\n"   for  $diff->Items(2);
		}
		$diff->Reset();
		my $p = 1;
		while(  $diff->Next()  ) {
			if ( $diff->Same() ) {
				$p += scalar $diff->Items(1);
				next;
			}
			print "; iptables -t $table -D $chain $p # $_\n" for $diff->Items(1);
			print "; iptables -t $table -I $chain ",$p++," $_\n" for $diff->Items(2);
		}
	}
}

sub compare_tables {
	my $table = shift;
	my $t1 = shift;
	my $t2 = shift;

	my %all_chains;
	$all_chains{$_}++ for keys %{$t1};
	$all_chains{$_}++ for keys %{$t2};

	for my $chain ( sort keys %all_chains ) {
		compare_chains( $table, $chain, $t1->{$chain}, $t2->{$chain} );
	}
}

sub compare_rulesets {
	my $r1 = shift;
	my $r2 = shift;

	my %all_tables;
	$all_tables{$_}++ for keys %{$r1};
	$all_tables{$_}++ for keys %{$r2};

	for my $table ( sort keys %all_tables ) {
		compare_tables( $table, $r1->{$table}, $r2->{$table} );
	}
}

my $r1 = parse_ruleset( $ARGV[0] );
my $r2 = parse_ruleset( $ARGV[1] );

compare_rulesets( $r1, $r2 );
